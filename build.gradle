buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

project.ext.buildnumber = "undefined"
if (System.getenv().BUILD_NUMBER) project.buildnumber = System.getenv().BUILD_NUMBER

version = "1.7.10-1.0.0-alpha.1-build." + project.buildnumber
group = "net.angrypixel"
archivesBaseName = 'TheBetweenlands'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

minecraft {
    version = "1.7.10-10.13.4.1448-1.7.10"
    runDir = "minecraft"
    replace '${version}', project.version
    replace '/*!*/true/*!*/', 'false'
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
        java { 
            srcDir 'java' 
        }
        resources { 
            srcDir 'resources' 
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    classifier = 'universal'
    manifest {
        attributes('FMLCorePlugin': 'thebetweenlands.core.TheBetweenlandsLoadingPlugin')
        attributes('FMLCorePluginContainsFMLMod': 'true')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'src'
    from sourceSets.main.allSource
}

task devJar(type: Jar) {
    classifier = 'dev'
    from sourceSets.main.output
}

artifacts {
    archives sourcesJar, devJar
}
